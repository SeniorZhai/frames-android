apply plugin: 'com.android.application'

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "checkout.checkout_android"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled = true
    }
    signingConfigs {
        release {
            storeFile file('checkout-frames-local-signature.jks')
            storePassword '123456'
            keyAlias 'key0'
            keyPassword '123456'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // Create baseline for lint check to ignore old issues.
    lintOptions {
        baseline(project.file("lint-baseline.xml"))
    }
}

dependencies {
    // Frames module dependency
    implementation FramesConfig.framesAndroidDependency

    implementation 'androidx.appcompat:appcompat:1.2.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestUtil 'androidx.test:orchestrator:1.4.1'

    ext.okHttpVersion = '4.9.0'
    implementation 'com.squareup.okhttp3:okhttp:' + ext.okHttpVersion
    implementation 'com.squareup.okhttp3:logging-interceptor:' + ext.okHttpVersion
}

configurations.all {
    resolutionStrategy.dependencySubstitution {
        if (FramesConfig.useLocalModuleDependencies) {

            substitute(module("${FramesConfig.productGroupId}:${FramesConfig.productArtefactId}"))
                    .using(project(":android-sdk"))
                    .because("Development and QA validation is performed using the local project module")
        }
    }
}